SET QUOTED_IDENTIFIER ON
GO
SET ANSI_NULLS ON
GO

CREATE PROCEDURE [dbo].[sp_VOL_NUMChanged_l]
	@MODIFIED_DATE smalldatetime
WITH EXECUTE AS CALLER
AS
SET NOCOUNT ON

/*
	Checked for Release: 3.6
	Checked by: CL
	Checked on: 27-Sep-2014
	Action: TESTING REQUIRED
*/

;WITH records (NUM, LangID, ORG_NAME_FULL, NON_PUBLIC, DELETION_DATE, RECORD_OWNER, OpportunityCount)
AS (
SELECT	btd.NUM, btd.LangID,
		dbo.fn_GBL_DisplayFullOrgName_2(btd.NUM,btd.ORG_LEVEL_1,btd.ORG_LEVEL_2,btd.ORG_LEVEL_3,btd.ORG_LEVEL_4,btd.ORG_LEVEL_5,btd.LOCATION_NAME,btd.SERVICE_NAME_LEVEL_1,btd.SERVICE_NAME_LEVEL_2,bt.DISPLAY_LOCATION_NAME,bt.DISPLAY_ORG_NAME) AS ORG_NAME_FULL,
		btd.NON_PUBLIC, btd.DELETION_DATE,
		vo.RECORD_OWNER,
		COUNT(DISTINCT vo.VNUM) AS OpportunityCount
	FROM VOL_Opportunity vo
	INNER JOIN VOL_Opportunity_Description vod
		ON vo.VNUM=vod.VNUM
			AND (vod.DELETION_DATE IS NULL OR vod.DELETION_DATE < GETDATE())
			AND (vo.DISPLAY_UNTIL IS NULL OR vo.DISPLAY_UNTIL >=  GETDATE())
	INNER JOIN GBL_BaseTable bt
		ON bt.NUM=vo.NUM
	INNER JOIN GBL_BaseTable_Description btd
		ON btd.NUM=bt.NUM AND btd.LangID=(SELECT TOP 1 LangID FROM GBL_BaseTable_Description WHERE NUM=btd.NUM ORDER BY CASE WHEN LangID=vod.LangID THEN 0 ELSE 1 END, LangID)
WHERE EXISTS(SELECT * FROM GBL_BaseTable_History h
		INNER JOIN GBL_FieldOption fo
			ON h.FieldID=fo.FieldID AND fo.FieldName IN ('NON_PUBLIC','DELETION_DATE')
		WHERE h.NUM=btd.NUM AND h.LangID=btd.LangID
			AND h.MODIFIED_DATE > @MODIFIED_DATE)
GROUP BY btd.NUM, btd.LangID,
	btd.ORG_LEVEL_1, btd.ORG_LEVEL_2, btd.ORG_LEVEL_3, btd.ORG_LEVEL_4, btd.ORG_LEVEL_5, btd.LOCATION_NAME, btd.SERVICE_NAME_LEVEL_1, btd.SERVICE_NAME_LEVEL_2, bt.DISPLAY_LOCATION_NAME, bt.DISPLAY_ORG_NAME,
	btd.NON_PUBLIC, btd.DELETION_DATE,
	vo.RECORD_OWNER
)
SELECT a.UpdateEmailVOL, m.DefaultEmailVOL, m.DefaultEmailNameVOL, r.*
	FROM records AS r
	INNER JOIN GBL_Agency a
		ON r.RECORD_OWNER=a.AgencyCode
	INNER JOIN STP_Member AS m
		ON m.MemberID = a.MemberID
WHERE a.UpdateEmailVOL IS NOT NULL
ORDER BY r.RECORD_OWNER, ORG_NAME_FULL, NUM, LangID

SET NOCOUNT OFF


GO
GRANT EXECUTE ON  [dbo].[sp_VOL_NUMChanged_l] TO [cioc_maintenance_role]
GO
