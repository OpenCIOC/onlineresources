SET QUOTED_IDENTIFIER ON
GO
SET ANSI_NULLS ON
GO

CREATE PROCEDURE [dbo].[sp_CIC_NUMSetFTIDs_u]
	@NUM varchar(8),
	@IdList varchar(max)
WITH EXECUTE AS CALLER
AS
SET NOCOUNT ON

/*
	Checked for Release: 3.1
	Checked by: KL
	Checked on: 06-Apr-2012
	Action: NO ACTION REQUIRED
	Notes: For future, incoporate MERGE statement
*/

DECLARE @tmpFTIDs TABLE(
	FT_ID int NOT NULL PRIMARY KEY
)

INSERT INTO @tmpFTIDs SELECT DISTINCT tm.*
	FROM dbo.fn_GBL_ParseIntIDList(@IdList,',') tm
	INNER JOIN CIC_FeeType ft
		ON tm.ItemID=ft.FT_ID

DELETE pr
	FROM CIC_BT_FT pr
	LEFT JOIN @tmpFTIDs tm
		ON pr.FT_ID = tm.FT_ID
WHERE tm.FT_ID IS NULL AND NUM=@NUM

INSERT INTO CIC_BT_FT (NUM, FT_ID) SELECT NUM=@NUM, tm.FT_ID
	FROM @tmpFTIDs tm
WHERE NOT EXISTS(SELECT * FROM CIC_BT_FT pr WHERE NUM=@NUM AND pr.FT_ID=tm.FT_ID)

SET NOCOUNT OFF




GO
GRANT EXECUTE ON  [dbo].[sp_CIC_NUMSetFTIDs_u] TO [cioc_login_role]
GO
