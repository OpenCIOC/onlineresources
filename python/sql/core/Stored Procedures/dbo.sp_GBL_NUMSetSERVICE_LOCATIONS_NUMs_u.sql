SET QUOTED_IDENTIFIER ON
GO
SET ANSI_NULLS ON
GO



CREATE PROCEDURE [dbo].[sp_GBL_NUMSetSERVICE_LOCATIONS_NUMs_u]
	@NUM varchar(8),
	@IdList varchar(max)
WITH EXECUTE AS CALLER
AS
SET NOCOUNT ON

/*
	Checked for Release: 3.5
	Checked by: KL
	Checked on: 20-Jan-2012
	Action: IN PROGRESS
*/

DECLARE @tmpLOCATION_NUMs TABLE(
	LOCATION_NUM varchar(8) COLLATE Latin1_General_100_CI_AI NOT NULL PRIMARY KEY 
)

INSERT INTO @tmpLOCATION_NUMs SELECT DISTINCT tm.*
	FROM dbo.fn_GBL_ParseVarCharIDList(@IdList,',') tm
	INNER JOIN GBL_BaseTable bt
		ON tm.ItemID=bt.NUM COLLATE Latin1_General_100_CI_AI 
			AND bt.NUM<>@NUM COLLATE Latin1_General_100_CI_AI
			
MERGE INTO GBL_BT_LOCATION_SERVICE dst
USING @tmpLOCATION_NUMs src
ON dst.LOCATION_NUM=src.LOCATION_NUM AND dst.SERVICE_NUM=@NUM
WHEN NOT MATCHED BY TARGET THEN
	INSERT (SERVICE_NUM, LOCATION_NUM)
		VALUES (@NUM, src.LOCATION_NUM)
WHEN NOT MATCHED BY SOURCE AND dst.SERVICE_NUM=@NUM THEN
	DELETE
	;
	

SET NOCOUNT OFF


GO
GRANT EXECUTE ON  [dbo].[sp_GBL_NUMSetSERVICE_LOCATIONS_NUMs_u] TO [cioc_login_role]
GO
